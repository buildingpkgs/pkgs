# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=6.14.0
pkgrel=2
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL-2.0-only LGPL-3.0-only)
depends=(acl
         gcc-libs
         glibc
         karchive
         kauth
         kbookmarks
         kcolorscheme
         kcompletion
         kconfig
         kcoreaddons
         kcrash
         kdbusaddons
         kguiaddons
         ki18n
         kiconthemes
         kitemviews
         kjobwidgets
         kservice
         kwallet
         kwidgetsaddons
         kwindowsystem
         libxml2
         libxslt
         qt6-base
         solid
         util-linux-libs)
makedepends=(doxygen
             extra-cmake-modules
             kdoctools
             qt6-doc
             qt6-tools)
optdepends=('kded: proxy management and cookie storage'
            'kdoctools: for the help kioslave'
            'kio-extras: extra protocols support (sftp, fish and more)'
            'kio-fuse: to mount remote filesystems via FUSE'
            'switcheroo-control: hybrid GPU support')
groups=(kf6)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        https://invent.kde.org/frameworks/kio/-/merge_requests/1764.patch)
sha256sums=('9b03746fd008504a96f569f37ad8ff902cc71495e7e123daa3c6de79ff2acc45'
            '7a04de66ff92b2c8754f2e7e0e4e61f5e029a842025eee12de623f56b78a8b51')

prepare() {
  patch -d $pkgname-$pkgver -p1 < 1764.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
